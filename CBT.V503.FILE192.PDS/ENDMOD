         MACRO                                                             1
         ENDMOD                                                            2
.*+-------------------------------------------------------------------+    3
.*¦                     MAINTENANCE LOG                               ¦    4
.*+----------+------------------------------------+---------+---------+    5
.*¦  DATE    ¦      DESCRIPTION                   ¦ CHANGED ¦ REFER   ¦    6
.*+----------+------------------------------------+---------+---------+    7
.*¦ 02/19/92 ¦ USE REGISTER R1 INSTEAD OF R13     ¦ MJM     ¦ #01     ¦    8
.*¦          ¦   IN STORAGE RELEASE ADDR= OPERAND ¦         ¦         ¦    9
.*¦          ¦   (FIX#-R0001)                     ¦         ¦         ¦   10
.*¦ 07/14/92 ¦ TYPE=ESTAEX/PCRETRY= SUPPORT       ¦ MJM     ¦ #02     ¦   11
.*¦          ¦   (FIX#-R0007)                     ¦         ¦         ¦   12
.*+----------+------------------------------------+---------+---------+   13
.*¦      (C)   COPYRIGHT MD-PALADIN INC. 1992                         ¦   14
.*+-------------------------------------------------------------------+   15
.*                                                                    ¦   16
.**********************************************************************   17
.*       END MODULE WITH THIS MACRO                                   *   18
.**********************************************************************   19
         GBLA  &POOL                                                      20
         GBLB  &CALSA,&WORKA,&FLOAT,&RENTA                                21
         GBLB  &RETRYA                                              #02   22
         GBLB  &ESTAEA                                              #02   23
         GBLC  &SYSSPLV,&SYSASCE                                          24
         GBLC  &DSCTLEN,&NRENTWK,&DNAME                                   25
         GBLC  &SAVEGEN,&SAVELEN,&SAVEFLT,&SPLEVI                         26
         GBLC  &GRSVRET                                             #02   27
         LCLC  &AROUND                                                    28
&AROUND  SETC  'AWK'.'&SYSNDX'                                            29
*---------------------------------------------------------------------*   30
*        COMMON EXIT                                                  *   31
*---------------------------------------------------------------------*   32
         AIF   (&FLOAT EQ 0).NOFLT                                        33
         LD    0,&SAVEFLT               .. RESTORE                        34
         LD    2,&SAVEFLT+8             ..FLOATING                        35
         LD    4,&SAVEFLT+16            ..POINT                           36
         LD    6,&SAVEFLT+24            ..REGISTERS                       37
.NOFLT   ANOP                                                             38
         AIF   (&RETRYA EQ 0).NRETY                                 #02   39
         LM    0,1,&GRSVRET       LOAD GENERAL REGS 0 AND 1         #02   40
         LAM   0,1,&GRSVRET+8     LOAD ACCESS  REGS 0 AND 1         #02   41
         MSTA  0                  PUT ON LINKAGE STACK              #02   42
.NRETY   ANOP                                                       #02   43
         AIF   (&WORKA EQ 0).NOWORK                                       44
         AIF   (&RENTA EQ 1).YRENT                                        45
         B     &AROUND                  BRANCH AROUND WORK AREA           46
         CNOP  0,8                      FORCE DOUBLEWORD ALIGNMENT        47
&NRENTWK DS    CL(&DSCTLEN)             WORK AREA                         48
&AROUND  DS    0H                                                         49
         AGO   .NOWORK                                                    50
.YRENT   ANOP                                                             51
         L     0,&SAVELEN               LENGTH OF STORAGE                 52
         LR    1,13                     A(SAVE AREA) => R2          #01   53
         AIF   ('&SPLEVI' GT '2').ESA1                              #01   54
         AIF   (&CALSA EQ 0).NOCSA                                        55
         L     13,&SAVEGEN+4            A(CALLER'S SAVE AREA)             56
*                                       FREE WORK AREA STORAGE            57
.NOCSA   ANOP                                                             58
         LR    2,15                                                       59
         FREEMAIN RU,                                                  X  60
               LV=(0),                                                 X  61
               SP=&POOL,                                               X  62
               A=(1)                                                      63
         LR    15,2                                                       64
         AGO   .NOWORK                                                    65
.ESA1    ANOP                                                             66
         LR    2,15                     RETURN CODE => R2                 67
*                                       FREE WORK AREA STORAGE            68
         STORAGE RELEASE,COND=NO,                                      X  69
               LENGTH=(0),                                             X  70
               ADDR=(1),                                            #01X  71
               SP=&POOL                                                   72
         LR    15,2                     RETURN CODE => R15                73
.NOWORK  ANOP                                                             74
         AIF   ('&SPLEVI' LT '3').NOESA1                                  75
         AIF   (&ESTAEA EQ 1).SESTAE                                #02   76
         LA    0,3                      ..RESTORE REGS 0 AND 1            77
         ESTA  0,0                      ..FROM STACK MODIFIABLE AREA      78
         AGO   .ISPR                                                #02   79
.SESTAE  ANOP                                                       #02   80
         EREG  0,1                      RESTORE REGS 0 AND 1        #02   81
.ISPR    ANOP                                                       #02   82
         PR                             RESTORE REGS AND RETURN           83
         AGO   .MEND                                                      84
.NOESA1  ANOP                                                             85
         AIF   (&CALSA EQ 0).NOCSA1                                       86
         ST    15,16(,13)                                                 87
         LM    14,12,12(13)             RESTORE CALLER'S REGISTERS        88
.NOCSA1  ANOP                                                             89
         AIF   ('&SYSSPLV' EQ '1').NOXA1                                  90
         BSM   0,14                     RESTORE AMODE AND RETURN          91
         AGO   .MEND                                                      92
.NOXA1   ANOP                                                             93
         BR    14                       RETURN TO CALLER                  94
         AGO   .MEND                                                      95
*        *---------------------------------------*                        96
*        *     LITERALS                          *                        97
*        *---------------------------------------*                        98
.MEND    ANOP                                                             99
         LTORG                                                           100
         MEND                                                            101
