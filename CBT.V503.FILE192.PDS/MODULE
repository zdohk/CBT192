         MACRO                                                             1
         MODULE &CNAME,&AMODE=31,&RMODE=ANY,&SP=1,&LOC=,&BASE=12,      X   2
               &ENTRY=BRANCH,&FLOATSV=NO,&RENT=YES,&WORK=YES,          X   3
               &PCRETRY=,&SDWA=3,                                   #01X   4
               &TEXT=' '                                                   5
.*+-------------------------------------------------------------------+    6
.*¦                     MAINTENANCE LOG                               ¦    7
.*+----------+------------------------------------+---------+---------+    8
.*¦  DATE    ¦      DESCRIPTION                   ¦ CHANGED ¦ REFER   ¦    9
.*+----------+------------------------------------+---------+---------+   10
.*¦ 05/27/92 ¦ ENTRY=ESTAEX/PCRETRY= SUPPORT      ¦ MJM     ¦ #01     ¦   11
.*¦ 08/02/92 ¦ EXTENDED ARR PARMLIST              ¦ MJM     ¦ #02     ¦   12
.*+----------+------------------------------------+---------+---------+   13
.*¦      (C)   COPYRIGHT MD-PALADIN INC. 1992                         ¦   14
.*+-------------------------------------------------------------------+   15
.*                                                                        16
.**********************************************************************   17
.*   BEGIN MODULE WITH THIS MACRO                                     *   18
.*     &CNAME   - NAME OF MODULE (7 CHARACTERS OR LESS)               *   19
.*     &AMODE   - "24" OR "31"                                        *   20
.*     &RMODE   - "24" OR "ANY"                                       *   21
.*     &SP      - SUBPOOL FOR RENTRANT STORAGE THIS MODULE            *   22
.*     &LOC     - "BELOW" IF GETMAINED STORAGE BELOW 16M              *   23
.*     &BASE    - BASE REGISTER (NOT R1, R2 OR R13)                   *   24
.*     &ENTRY   - "BRANCH" "PCSTACK" "PCBASIC" "SRB" OR "SVC"         *   25
.*                OR "ESTAEX" .   "ESTAEX" IS ALSO FOR ARR RTNS     #01   26
.*     &FLOATSV - SAVE FLOATING POINT REGISTERS                       *   27
.*     &RENT    - WORK AREA DYNAMICALLY OBTAINED (DEFAULT = YES)      *   28
.*     &WORK    - WORK AREA TO BE GOTTEN (DEFAULT = YES)              *   29
.*     &PCRETRY - RETRY ADDRESS FOR ARR. (WHEN ENTRY=PCSTACK ONLY)  #01   30
.*     &SDWA    - REGISTER USED TO ADDRESS SDWA (WHEN TYPE=ESTAEX)  #01   31
.*     &TEXT    - COMMENT THAT IS ASSEMBLED INTO OBJECT CODE          *   32
.**********************************************************************   33
         GBLA  &POOL                                                      34
         GBLB  &EQU                                                       35
         GBLB  &CALSA,&WORKA,&FLOAT,&RENTA                                36
         GBLB  &RETRYA                                              #01   37
         GBLB  &ESTAEA                                              #01   38
         GBLC  &SYSSPLV,&SYSASCE                                          39
         GBLC  &DSCTLEN,&NRENTWK,&DNAME                                   40
         GBLC  &SAVEGEN,&SAVELEN,&SAVEFLT,&SPLEVI                         41
         GBLC  &GRSVRET                                             #01   42
         LCLB  &E1                                                        43
         LCLC  &BEGIN,&TXT,&ONSTACK                                       44
.*#02    LCLC  &RETPARM                                             #01   45
         LCLC  &ESTAE1                                              #01   46
         LCLC  &CNAMEL                                              #02   47
         SPLEVEL  TEST                                                    48
.*************************************************                        49
.*       CHECK ERROR CONDITIONS                  *                        50
.*************************************************                        51
&E1      SETB  0                                                          52
         AIF   (T'&CNAME NE 'O').OK1                                      53
         MNOTE 8,'MODULE NAME IS REQUIRED'                                54
&E1      SETB  1                                                          55
.OK1     ANOP                                                             56
         AIF   (K'&CNAME LE 7).OK2                                        57
         MNOTE 8,'MODULE NAME MUST BE LESS THAN 7 CHARACTERS'             58
&E1      SETB  1                                                          59
.OK2     ANOP                                                             60
         AIF   (&BASE GT 2 AND &BASE LT 13).OK3                           61
         MNOTE 8,'ONLY REGISTERS 3 THROUGH 12 CAN BE BASE REGISTERS'      62
&E1      SETB  1                                                          63
.OK3     ANOP                                                             64
         AIF   ('&ENTRY' EQ 'BRANCH' OR '&ENTRY' EQ 'PCSTACK').OK4        65
         AIF   ('&ENTRY' EQ 'PCBASIC' OR '&ENTRY' EQ 'SRB').OK4           66
         AIF   ('&ENTRY' EQ 'SVC').OK4                                    67
         AIF   ('&ENTRY' EQ 'ESTAEX').OK4                           #01   68
         MNOTE 8,'"ENTRY="  "BRANCH","PCSTACK","PCBASIC","SRB","SVC"'     69
         MNOTE 8,' OR  "ESTAEX" '                                   #01   70
&E1      SETB  1                                                          71
.OK4     ANOP                                                             72
         AIF   ('&ENTRY' NE 'PCSTACK' OR '&SYSSPLV' GT '2').OK5           73
         MNOTE 8,'"ENTRY=PCSTACK" MUST BE ASSEMBLED "SPLEVEL > 2"  '      74
&E1      SETB  1                                                          75
.OK5     ANOP                                                             76
         AIF   ('&ENTRY' NE 'SVC').OK6                                    77
         AIF   ('&BASE' EQ '6').OK6                                       78
         MNOTE 8,'BASE REGISTER MUST BE 6 WITH "ENTRY=SVC" '              79
&E1      SETB  1                                                          80
.OK6     ANOP                                                             81
         AIF   (&E1).MEND                                                 82
.*************************************************                        83
.*       SET GLOBAL SYMBOLS                      *                        84
.*************************************************                        85
&POOL    SETA  &SP                                                        86
&SPLEVI  SETC  '&SYSSPLV'                                                 87
&DSCTLEN SETC  'L'.'&CNAME'                                               88
&NRENTWK SETC  'N'.'&CNAME'                                               89
&DNAME   SETC  'W'.'&CNAME'                                               90
&BEGIN   SETC  'BEG'.'&SYSNDX'                                            91
&SAVEGEN SETC  'GEN'.'&SYSNDX'                                            92
&SAVEFLT SETC  'FLT'.'&SYSNDX'                                            93
&SAVELEN SETC  'LEN'.'&SYSNDX'                                            94
&ONSTACK SETC  'STK'.'&SYSNDX'                                            95
&GRSVRET SETC  'GRT'.'&SYSNDX'                                      #01   96
.*#02RETPARM SETC  'RET'.'&SYSNDX'                                  #01   97
&ESTAE1  SETC  'EST'.'&SYSNDX'                                      #01   98
&CNAMEL  SETC  'CNA'.'&SYSNDX'                                      #02   99
&TXT     SETC  '&TEXT'                                                   100
         AIF   ('&TXT'(1,1) EQ '''').STRIP                               101
         AIF   ('&TXT'(1,1) EQ '(').STRIP                                102
         AGO   .NOSTRIP                                                  103
.STRIP   ANOP                                                            104
&TXT     SETC  '&TXT'(2,K'&TXT-2)                                        105
.NOSTRIP ANOP                                                            106
.*************************************************                       107
.*       SET GLOBAL SWITCHES                     *                       108
.*************************************************                       109
&WORKA   SETB  0     OBTAIN REENTRANT WORKAREA ?                         110
         AIF   ('&WORK'(1,1) EQ 'N').NWORK                               111
         AIF   ('&ENTRY' EQ 'PCBASIC' AND '&SPLEVI' LT '3').NWORK        112
         AIF   ('&ENTRY' EQ 'SVC' AND '&SPLEVI' LT '3').NWORK            113
&WORKA   SETB  1                                                         114
.NWORK   ANOP                                                            115
&RENTA   SETB  0     OBTAIN WORK AREA AND NON-REENTRANT                  116
         AIF   ('&RENT'(1,1) EQ 'N').NRENT                               117
         AIF   ('&ENTRY' EQ 'SRB' AND '&SPLEVI' LT '3').NRENT            118
&RENTA   SETB  1                                                         119
.NRENT   ANOP                                                            120
&CALSA   SETB  0     IS SAVE AREA PROVIDED BY CALLER ?                   121
         AIF   ('&ENTRY'(1,2) EQ 'PC').NSAVE                             122
         AIF   ('&ENTRY' EQ 'SRB').NSAVE                                 123
         AIF   ('&ENTRY' EQ 'SVC').NSAVE                                 124
&CALSA   SETB  1                                                         125
.NSAVE   ANOP                                                            126
&FLOAT   SETB  0     ARE FLOATING POINT REGISTERS SAVED ?                127
         AIF   ('&FLOATSV'(1,1) EQ 'N').NFLOT                            128
         AIF   (&WORKA EQ 0).NFLOT                                       129
&FLOAT   SETB  1                                                         130
.NFLOT   ANOP                                                            131
&RETRYA  SETB  0     RETRY                                          #01  132
         AIF   (T'&PCRETRY EQ 'O').NRTTT                            #01  133
         AIF   ('&ENTRY' NE 'PCSTACK').NRTTT                        #01  134
&RETRYA  SETB  1                                                    #01  135
.NRTTT   ANOP                                                       #01  136
&ESTAEA  SETB  0     ESTAE ?                                        #01  137
         AIF   ('&ENTRY' NE 'ESTAEX').NESTT                         #01  138
         AIF   ('&SPLEVI' LT '3').NESTT                             #01  139
&ESTAEA  SETB  1                                                    #01  140
.NESTT   ANOP                                                       #01  141
&CNAME   CSECT                                                           142
         AIF   ('&SPLEVI' LT '2').NOAMODE                                143
*        *---------------------------------------*                       144
*        *     SPECIFY AMODE AND RMODE           *                       145
*        *---------------------------------------*                       146
&CNAME   AMODE &AMODE                                                    147
&CNAME   RMODE &RMODE                                                    148
.NOAMODE ANOP                                                            149
         AIF   (&WORKA NE 1).NOWORK0                                     150
*        +---------------------------------------+                       151
*        ¦     SAVE AREAS                        ¦                       152
*        +---------------------------------------+                       153
&DNAME   DSECT                    USER ACQUIRED STORAGE FOR REENTRABLE   154
&SAVEGEN DS    18F                GENERAL REGISTER SAVE AREA             155
         AIF   (&FLOAT NE 1).NOF1                                        156
&SAVEFLT DS    4D                 FLOATING POINT REGISTER SAVE AREA      157
.NOF1    ANOP                                                            158
&SAVELEN DS    F                  LENGTH OF ACQUIRED STORAGE             159
         AIF   (&RETRYA EQ 0).NORET                                 #01  160
&GRSVRET DS    4F                 GENERAL AND ACCESS REGS 0 AND 1   #01  161
ESTPARAM DS    0F,CL(LESTPARM)    ARR RETRY ADDRESS AND ALET        #02  162
.*#02    DS    X                  ARR FLAGS BYTE                    #01  163
.*#02    DS    3X                 NOT USED                          #01  164
         ESTPARM                                                    #02  165
.NORET   ANOP                                                       #01  166
&CNAME   CSECT                                                           167
.NOWORK0 ANOP                                                            168
         AIF   (&EQU).NOEQU                                              169
&EQU     SETB  1                                                         170
*        *---------------------------------------*                       171
*        *     REGISTER EQUATES                  *                       172
*        *---------------------------------------*                       173
         PRINT NOGEN                                                     174
         COPY EQUATES                                                    175
         PRINT GEN                                                       176
.NOEQU   ANOP                                                            177
*        *---------------------------------------*                       178
*        *     ENTRY POINT                       *                       179
*        *---------------------------------------*                       180
         AIF   ('&ENTRY'(1,2) EQ 'PC').PCENT                             181
         AIF   ('&ENTRY' EQ 'SVC').SVCENT                                182
         AIF   ('&ENTRY' EQ 'ESTAEX').ESTAENT                       #01  183
.*************************************************                       184
.*       ENTRY = BRANCH AND SRB                  *                       185
.*************************************************                       186
         USING &CNAME,15          SET ADDRESSING ON ENTRY POINT          187
         B     &BEGIN             BRANCH AROUND EYECATCHER               188
         DC    C' &CNAME '        NAME OF CSECT                          189
         DC    C' &SYSDATE '      DATE OF ASSEMBLY                       190
         DC    C' &SYSTIME '      TIME OF ASSEMBLY                       191
         DC    C' &TXT  '                                                192
         AIF   ('&SPLEVI' LT '3').XA1                                    193
         AIF   (&WORKA EQ 0).NL1                                         194
&ONSTACK DC    C'F1SA'            CALLER'S SAVE AREA ON STACK            195
.NL1     ANOP                                                            196
&BEGIN   BAKR  14,0               PUSH REGS AND PSW ONTO STACK           197
         LAE   &BASE,0(15,0)      LOAD BASE REGISTER WITH ENTRYPNT       198
         MSTA  0                  PUSH REGS AND PSW ONTO STACK           199
         AGO   .DROP15            DROP ADDRESSING ON ENTRY POINT         200
.XA1     ANOP                                                            201
         AIF   (&CALSA NE 1).NOSA1                                       202
&BEGIN   SAVE  (14,12)            CALLER'S REGS => CALLER'S SAVEAREA     203
         LR    &BASE,15           LOAD BASE REGISTER WITH ENTRY POINT    204
         AGO   .DROP15            DROP ADDRESSING ON ENTRY POINT         205
.NOSA1   ANOP                                                            206
&BEGIN   LR    &BASE,15           LOAD BASE REGISTER WITH ENTRY POINT    207
.DROP15  ANOP                                                            208
         DROP  15                 DROP ADDRESSING ON ENTRY POINT         209
         USING &CNAME,&BASE       SET ADDRESSING ON BASE REGISTER        210
         AGO   .CKSTG                                                    211
.*************************************************                  #01  212
.*       ENTRY = ESTAEX (ALSO FOR ARRS)          *                  #01  213
.*************************************************                  #01  214
.ESTAENT ANOP                                                       #01  215
         PUSH  PRINT                                                #01  216
         PRINT NOGEN                                                #01  217
         IHASDWA                                                    #01  218
         POP   PRINT                                                #01  219
&CNAME   CSECT                                                      #01  220
         USING &CNAME,15          SET ADDRESSING ON ENTRY POINT     #01  221
         B     &BEGIN             BRANCH AROUND EYECATCHER          #01  222
         DC    C' &CNAME '        NAME OF CSECT                     #01  223
         DC    C' &SYSDATE '      DATE OF ASSEMBLY                  #01  224
         DC    C' &SYSTIME '      TIME OF ASSEMBLY                  #01  225
         DC    C' &TXT  '                                           #01  226
         AIF   (&WORKA EQ 0).NL8                                    #01  227
&ONSTACK DC    C'F1SA'            CALLER'S SAVE AREA ON STACK       #01  228
.NL8     ANOP                                                       #01  229
&BEGIN   DS    0H                 PUSH REGS AND PSW ONTO STACK      #01  230
*        *---------------------------------------*                  #01  231
*        *     TEST WHETHER THERE IS AN SDWA     *                  #01  232
*        *---------------------------------------*                  #01  233
         CH    R0,=H'12'               WAS AN SDWA OBTAINED ?       #01  234
         BNE   &ESTAE1                 YES; CONTINUE                #01  235
         LA    R15,0                   SET FOR PERCOLATION          #01  236
         BR    R14                     RETURN TO RTM2               #01  237
&ESTAE1  DS    0H                                                   #01  238
         BAKR  14,0               PUSH REGS AND PSW ONTO STACK      #01  239
         LAE   &BASE,0(15,0)      LOAD BASE REGISTER WITH ENTRYPNT  #01  240
         MSTA  0                  PUSH REGS AND PSW ONTO STACK      #01  241
         DROP  15                 DROP ADDRESSING ON ENTRY POINT    #01  242
         USING &CNAME,&BASE       SET ADDRESSING ON BASE REGISTER   #01  243
         AGO   .CKSTG                                               #01  244
.*************************************************                       245
.*       ENTRY = PCSTACK AND PCBASIC             *                       246
.*************************************************                       247
.PCENT   ANOP                                                            248
         BASR  &BASE,0            LOAD BASE REGISTER                #01  249
         BCTR  &BASE,0            .. SUBTRACT 2 FROM                     250
         BCTR  &BASE,0            .. BASE REGISTER                       251
         USING &CNAME,&BASE       SET ADDRESSING ON ENTRY POINT          252
         AIF   ('&SPLEVI' LT '3').PC2                                    253
         LAE   &BASE,0(&BASE,0)   CLEAR BASE ACCESS REGISTER             254
         AIF   (&RETRYA EQ 1).PC2                                   #01  255
         MSTA  0                  SAVE REGS 1 AND 2 IN USER STACK        256
.PC2     ANOP                                                            257
         B     &BEGIN             BRANCH AROUND EYECATCHER               258
         DC    C' '                                                 #02  259
&CNAMEL  DC    CL8'&CNAME',C' '   NAME OF CSECT                     #02  260
         DC    C' &SYSDATE '      DATE OF ASSEMBLY                       261
         DC    C' &SYSTIME '      TIME OF ASSEMBLY                       262
         DC    C' &TXT  '                                                263
         AIF   (&WORKA EQ 0).NL2                                         264
&ONSTACK DC    C'F1SA'            CALLER'S SAVE AREA ON STACK            265
.NL2     ANOP                                                            266
&BEGIN   DS    0H                                                        267
         AGO   .CKSTG                                                    268
.SVCENT  ANOP                                                            269
.*************************************************                       270
.*       ENTRY = SVC                             *                       271
.*************************************************                       272
         USING &CNAME,6           SET ADDRESSING ON ENTRY POINT          273
         B     &BEGIN             BRANCH AROUND EYECATCHER               274
         DC    C' &CNAME '        NAME OF CSECT                          275
         DC    C' &SYSDATE '      DATE OF ASSEMBLY                       276
         DC    C' &SYSTIME '      TIME OF ASSEMBLY                       277
         DC    C' &TXT  '                                                278
         AIF   ('&SPLEVI' LT '3').XA3                                    279
         AIF   (&WORKA EQ 0).NL3                                         280
&ONSTACK DC    C'F1SA'            CALLER'S SAVE AREA ON STACK            281
.NL3     ANOP                                                            282
&BEGIN   BAKR  14,0               PUSH REGS AND PSW ONTO STACK           283
         MSTA  0                  PUSH REGS AND PSW ONTO STACK           284
         AGO   .ESVCE             DROP ADDRESSING ON ENTRY POINT         285
.XA3     ANOP                                                            286
&BEGIN   DS    0H                                                        287
.ESVCE   ANOP                                                            288
.CKSTG   ANOP                                                            289
.*************************************************                       290
.*      GET A WORK AREA FOR REENTRANCY           *                       291
.*************************************************                       292
         AIF   (&WORKA EQ 0).NOWORK                                      293
         AIF   (&RENTA EQ 1).YESRENT                                     294
         LA    13,&NRENTWK        ADDRESS OF WORK AREA                   295
         AGO   .MAPDSECT                                                 296
.YESRENT ANOP                                                            297
*        *---------------------------------------*                       298
*        *     GET STORAGE FOR WORK AREA         *                       299
*        *---------------------------------------*                       300
         LA    0,&DSCTLEN          LENGTH OF STORAGE                     301
         AIF   (T'&LOC EQ 'O' OR '&SPLEVI' LT '2').NOLOC                 302
         AIF   ('&SPLEVI' LT '3').NOSTOR                                 303
         STORAGE OBTAIN,COND=NO,                                       X 304
               LENGTH=(0),                                             X 305
               LOC=&LOC,                                               X 306
               SP=&POOL,                                               X 307
               ADDR=(1)                                                  308
         AGO   .GOT                                                      309
.NOSTOR  ANOP                                                            310
         GETMAIN RU,                                                   X 311
               LV=(0),                                                 X 312
               LOC=&LOC,                                               X 313
               SP=&POOL                                                  314
         AGO   .GOT                                                      315
.NOLOC   ANOP                                                            316
         AIF   ('&SPLEVI' LT '3').NOSTOR1                                317
         STORAGE OBTAIN,COND=NO,                                       X 318
               LENGTH=(0),                                             X 319
               SP=&POOL,                                               X 320
               ADDR=(1)                                                  321
         AGO   .GOT                                                      322
.NOSTOR1 ANOP                                                            323
         GETMAIN RU,                                                   X 324
               LV=(0),                                                 X 325
               SP=&POOL                                                  326
.GOT     ANOP                                                            327
         AIF   ('&SPLEVI' GT '2').ESA2                                   328
         AIF   (&CALSA EQ 0).NOCSA0                                      329
         LR    2,13               PRESERVE ADDRESS CALLER'S SAVEAREA     330
         ST    13,4(1)            SAVE A(CALLER'S SAVE AREA)             331
         ST    1,8(13)            A(SAVE AREA) => CALLER'S SAVE AREA     332
.NOCSA0  ANOP                                                            333
         LR    13,1               A(SAVE AREA THIS CSECT) => R13         334
         AGO   .MAPDSECT                                                 335
.ESA2    ANOP                                                            336
         AIF   (&CALSA EQ 0).NOCSA                                       337
         ST    1,8(0,13)          A(SAVE AREA) => CALLER'S SAVE AREA     338
.NOCSA   ANOP                                                            339
         LAE   13,0(1,0)          LOAD SAVE AREA GEN AND ACC REGS        340
*                                 INDICATE CALLER'S SAVEAREA ON STACK    341
         MVC   4(L'&ONSTACK,13),&ONSTACK                                 342
.MAPDSECT ANOP                                                           343
         USING &DNAME,13          ADDRESSING FOR SAVE AREA               344
         ST    0,&SAVELEN         SAVE SUBPOOL AND LENGTH                345
.*************************************************                       346
.*       RESTORE REGISTERS FROM STACK OR         *                       347
.*       CALLER'S SAVE AREA                      *                       348
.*************************************************                       349
.NOWORK  ANOP                                                            350
         AIF   ('&SPLEVI' LT '3').XA2                                    351
         AIF   (&RETRYA EQ 0).NRETY                                 #01  352
         EREG  0,1                RESTORE REGS FROM STACK           #01  353
         STM   0,1,&GRSVRET       SAVE GENERAL REGS 0 AND 1         #01  354
         STAM  0,1,&GRSVRET+8     SAVE ACCESS  REGS 0 AND 1         #01  355
         LAE   14,ESTPARAM        ARR PARMLIST ADDRESS              #02  356
         LAE   15,&PCRETRY               ..RETRY ADDRESS            #01  357
         ST    15,ESTRETRY-ESTPARM(14)   ..INTO ARR PARMLIST        #02  358
         ST    &BASE,ESTLOAD-ESTPARM(14) BASE REGISTER VALUE        #02  359
         MVC   ESTMOD-ESTPARM(L'&CNAMEL,14),&CNAMEL MODULE NAME     #02  360
         EAR   15,14              ARR PARMLIST ALET TO GPR 15       #02  361
.*#02    LAE   0,ESTPARAM         LOAD RETRY PARMLIST ADDRESS       #01  362
         MSTA  14                 SAVE IN MODIFIED STACK AREA       #01  363
.NRETY   ANOP                                                       #01  364
         EREG  14,1               RESTORE REGS FROM STACK                365
         AIF   ('&ENTRY' NE 'ESTAEX').NOSDWA                        #01  366
         LAE   &SDWA,0(0,1)       SDWA ADDRESS                      #01  367
         USING SDWA,&SDWA         ADDRESSIN FOR SDWA                #01  368
.NOSDWA  ANOP                                                       #01  369
         AGO   .NOCSA2                                                   370
.XA2     ANOP                                                            371
         AIF   (&CALSA EQ 0 OR &WORKA EQ 0).NOCSA2                       372
         LM    14,2,12(2)         RESTORE REGS FROM CALLER'S SAVEAREA    373
.NOCSA2  ANOP                                                            374
.******  SAVE FLOATING POINT REGISTERS ***********                       375
         AIF   (&FLOAT EQ 0).MEND                                        376
         STD   0,&SAVEFLT         ..SAVE                                 377
         STD   2,&SAVEFLT+8       ..FLOATING                             378
         STD   4,&SAVEFLT+16      ..POINT                                379
         STD   6,&SAVEFLT+24      ..REGISTERS                            380
.MEND    MEND                                                            381
